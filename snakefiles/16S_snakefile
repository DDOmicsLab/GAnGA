data_dir = config["outdir"]

current_directory = os.getcwd()
rule all:
    input: 
        expand(
            [
            os.path.join(data_dir, "{sample}/barrnap/{sample}_rrna.fa")
            
            ],sample=config["samples"]
        ),



rule Trimmomatic:
    input:
        valid_taxa=os.path.join(data_dir, "{sample}/logs/{sample}_validate_taxa.done"),
        r1 = lambda wc: config["samples"][wc.sample]["R1"],
        r2 = lambda wc: config["samples"][wc.sample]["R2"]
    output:
        fwd_paired   = os.path.join(data_dir, "{sample}", "00_trimmed_reads", "forward_paired.fastq.gz"),
        fwd_unpaired = os.path.join(data_dir, "{sample}", "00_trimmed_reads", "forward_unpaired.fastq.gz"),
        rev_paired   = os.path.join(data_dir, "{sample}", "00_trimmed_reads", "reverse_paired.fastq.gz"),
        rev_unpaired = os.path.join(data_dir, "{sample}", "00_trimmed_reads", "reverse_unpaired.fastq.gz")
    threads: 10
    resources:
        mem_mb = lambda wc, threads: threads * 2000
    params:
        extra = "SLIDINGWINDOW:4:15 MINLEN:36",
        xmx   = lambda wc, resources: f"-Xmx{resources.mem_mb // 1024}g"
    log:
        os.path.join(data_dir, "{sample}", "logs", "trimmomatic.log")
    conda:
        "envs/trimmomatic.yaml"
    shell:
        r"""
        echo ">>> Running Trimmomatic with {params.xmx}" > {log}
        echo ">>> Active conda env: $CONDA_PREFIX" >> {log}
        JAR=$(find $CONDA_PREFIX/share -name "trimmomatic.jar" | head -n1)
        if [ ! -s "$JAR" ]; then
            echo "ERROR: Could not find Trimmomatic jar in $CONDA_PREFIX/share" >> {log}
            exit 1
        fi
        java {params.xmx} -jar $JAR PE \
            -threads {threads} -phred33 \
            {input.r1} {input.r2} \
            {output.fwd_paired} {output.fwd_unpaired} \
            {output.rev_paired} {output.rev_unpaired} \
            {params.extra} \
            &>> {log}
        """   

rule Unicycler:
    input:
        trimmed_r1=os.path.join(data_dir, "{sample}/00_trimmed_reads/forward_paired.fastq.gz"),
        trimmed_r2=os.path.join(data_dir, "{sample}/00_trimmed_reads/reverse_paired.fastq.gz"),
    output:
        os.path.join(data_dir, "{sample}/02_unicycler/assembly.fasta"),
    params:
        outdir=directory(os.path.join(data_dir, "{sample}/02_unicycler")),
        options=lambda wildcards:config["parameters"]["Unicycler"],
    conda:
        "envs/unicycler.yaml"
    log:
        os.path.join(data_dir,"{sample}/logs/unicycler.log"),
    threads:
        40
    shell:
        """
        unicycler -1 {input.trimmed_r1} -2 {input.trimmed_r2} -o {params.outdir} {params.options} -t {threads} &> {log}
        """


rule Barrnap:
    input:
        os.path.join(data_dir, "{sample}/02_unicycler/assembly.fasta"),
    output:
        os.path.join(data_dir, "{sample}/barrnap/{sample}_rrna.fa"),
    params:
        options=lambda wildcards:config["parameters"]["Barrnap"]
    log:
        os.path.join(data_dir,"{sample}/logs/barrnap.log")
    threads:
        20
    shell:
        """
        barrnap -o {output} <{input} {params.options} --threads {threads} &> {log}
        """

